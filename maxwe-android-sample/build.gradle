apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.maxwe.android.project.sample"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "V1.0.0"
    }

    signingConfigs{
        release {
            storeFile file("../retechcorp-android-signature.jks")
            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }

        debug{
            //storeFile file("../retechcorp-android-signature.jks")
            //storePassword System.console().readLine("\nKeystore password: ")
            //keyPassword System.console().readLine("\nKey password: ")
            //keyPassword System.console().readLine("\nKEYALIAS password: ")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug{
            signingConfig signingConfigs.debug
            minifyEnabled true
            debuggable false
        }
    }

    productFlavors {
        local {
            applicationId "org.maxwe.android.project.sample.local"
            versionName "1.0-demo"
        }

        defaul {
            applicationId "org.maxwe.android.project.sample.defaul"
            versionName "1.0-full"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = "${variant.productFlavors[0].name}-${defaultConfig.versionName}-${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'AndroidManifest.xml'
        exclude 'res/drawable/ic_launcher.png'

    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    sourceSets.main.jniLibs.srcDirs = ['libs']

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
