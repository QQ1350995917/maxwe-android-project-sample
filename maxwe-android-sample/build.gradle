apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.maxwe.android.project.sample"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    signingConfigs{
        release {
            storeFile file("../key.android.maxwe.org.jks")
            storePassword System.getenv("KSTOREPWD")
            //storePassword System.console().readLine("\nKeystore password: ")
            keyAlias "key.android.maxwe.org"
            keyPassword System.getenv("KEYPWD")
            //keyPassword System.console().readLine("\nKey password: ")

        }
    }


    buildTypes {
        release {
//            signingConfig signingConfigs.release
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled false
            debuggable true
        }
    }

    productFlavors {
        demo {
            applicationId "org.maxwe.android.project.sample.demo"
            versionName "1.0-demo"
        }
        full {
            applicationId "org.maxwe.android.project.sample.full"
            versionName "1.0-full"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = "demo_${variant.productFlavors[0].name}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }


    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
    }

    sourceSets.main.jniLibs.srcDirs = ['libs']

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
